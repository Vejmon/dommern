plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.8'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.github.node-gradle.node' version('7.1.0')
}

group = 'no.vejmon'
version = '0.0.1-SNAPSHOT'
description = 'dommern'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    mockitoAgent
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation libs.bundles.spring.starters
    implementation libs.bundles.db
    implementation libs.bundles.pi4j

    implementation 'com.github.kwhat:jnativehook:2.2.2'
    implementation 'javazoom:jlayer:1.0.1'

    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation libs.bundles.test
    mockitoAgent(libs.mockito.core) {
        transitive = false
    }

}

tasks {
    test {
        systemProperty("spring.profiles.active", "test")
        jvmArgs += "-javaagent:${configurations.mockitoAgent.asPath}"
        useJUnitPlatform()
    }
}

tasks.register('npmDoInstall', NpmTask) {
    workingDir = file("${projectDir}/ui")
    args = ['install']
}

tasks.register('npmBuild', NpmTask) {
    dependsOn npmDoInstall
    workingDir = file("${projectDir}/ui")
    args = ['run', 'build']
}

tasks.register('copyVueBuild', Copy) {
    dependsOn npmBuild
    from "${projectDir}/ui/dist/*"
    into "${projectDir}/src/main/resources/static"
}

processResources {
    dependsOn rootProject.tasks.copyVueBuild
}

